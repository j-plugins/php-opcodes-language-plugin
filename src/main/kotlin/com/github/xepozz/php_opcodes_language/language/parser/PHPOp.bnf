{
  parserClass="com.github.xepozz.php_opcodes_language.language.parser.PHPOpParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="PHPOp"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.xepozz.php_opcodes_language.language.psi"
  psiImplPackage="com.github.xepozz.php_opcodes_language.language.psi.impl"
  psiImplUtilClass="com.github.xepozz.php_opcodes_language.language.psi.impl.PHPOpPsiImplUtil"

  elementTypeHolderClass="com.github.xepozz.php_opcodes_language.language.psi.PHPOpTypes"
  elementTypeClass="com.github.xepozz.php_opcodes_language.language.psi.PHPOpElementType"
  tokenTypeClass="com.github.xepozz.php_opcodes_language.language.psi.PHPOpTokenType"

  tokens = [
    LBRACE = "LBRACE {"
    RBRACE = "RBRACE }"
    LPAREN = "LPAREN ("
    RPAREN = "RPAREN )"
    LBRACKET = "LBRACKET ["
    RBRACKET = "RBRACKET ]"
    COMMENT = "COMMENT"
    EOL = "EOL"
    SYMBOL = "SYMBOL"
    IDENTIFIER = "IDENTIFIER"
    NUMBER = "NUMBER"
    DOLLAR_SIGN = "DOLLAR_SIGN"
    EQUALS_SIGN = "EQUALS_SIGN"
    TEXT = "TEXT"
    IDENTIFIER = "IDENTIFIER"
    COLON = "COLON"
  ]
}

// Top level structure
phpOpFile ::= item_*

private item_ ::= block | COMMENT | EOL

block ::= block_name COLON statements

block_name ::= var_name

statements ::= (statement | COMMENT EOL | EOL)+

statement ::= NUMBER (assignment_instruction | instruction)

assignment_instruction ::= IDENTIFIER EQUALS_SIGN instruction

instruction ::= instruction_name argument? argument?

instruction_name ::= IDENTIFIER

argument ::= expr

expr ::= var_name | string_literal | NUMBER | IDENTIFIER paren_expr | IDENTIFIER

string_literal ::= TEXT
{
    implements=["com.intellij.psi.PsiLanguageInjectionHost"]
    extends="com.github.xepozz.php_opcodes_language.language.psi.impl.PHPOpStringLiteralBaseImpl"
}

paren_expr ::= LPAREN expr RPAREN

var_name ::= DOLLAR_SIGN IDENTIFIER
{
  implements=["com.intellij.psi.NavigatablePsiElement" "com.intellij.psi.PsiNamedElement" "com.intellij.psi.PsiPolyVariantReference"]
  extends="com.github.xepozz.php_opcodes_language.language.psi.impl.PHPOpVarNameBaseImpl"
}